plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
        maven { url 'https://dl.bintray.com/alphagov/maven-test' }
        jcenter()
    }
    else {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
}

project.ext {
    version_number = '1.0.0'
    openSamlVersion = '3.3.0'
    verifyCommonUtils = '2.0.0-336'
    samlLibVersion = "$openSamlVersion-147"
    dropwizardVersion = '1.2.0'
}

dependencies {
    compile(
        "io.dropwizard:dropwizard-core:$dropwizardVersion",
        'org.json:json:20171018',
        "org.opensaml:opensaml-core:$openSamlVersion",
        "org.opensaml:opensaml-saml-impl:$openSamlVersion",
        "uk.gov.ida:saml-metadata-bindings:$samlLibVersion",
        "uk.gov.ida:common-utils:$verifyCommonUtils",
        "uk.gov.ida:saml-serializers:$samlLibVersion",
        "uk.gov.ida:saml-security:$samlLibVersion",
        "uk.gov.ida:saml-extensions:$samlLibVersion"
    )
    compile("commons-collections:commons-collections:3.2.2") { force = true }
    testCompile(
        'junit:junit:4.12',
        "io.dropwizard:dropwizard-testing:$dropwizardVersion",
        'org.mockito:mockito-core:2.12.0',
        "uk.gov.ida:saml-metadata-bindings-test:$samlLibVersion",
        "uk.gov.ida:saml-utils:$samlLibVersion",
        "uk.gov.ida:common-test-utils:2.0.0-43",
        "org.jsoup:jsoup:1.11.1",
    )
    testCompile('com.github.tomakehurst:wiremock:2.11.0'){ transitive = false }
}

sourceSets {
    acceptanceTest {
        java {
            srcDir 'src/acceptance-test/java'
        }
        resources {
            srcDir 'src/acceptance-test/resources'
        }
        compileClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.test.runtimeClasspath
        compileClasspath += sourceSets.test.output
    }
}

version = "$version_number"

distributions {
    main {
        contents {
            from {
                ['README.md', 'RELEASE_NOTES.md', 'verify-service-provider.yml']
            }
        }
    }
}

jar {
    manifest {
        attributes(
                "Version": version
        )
    }
}

task testAcceptance(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath += sourceSets.acceptanceTest.runtimeClasspath
}

run {
    args = ["server", "verify-service-provider.yml"]
}

mainClassName = 'uk.gov.ida.verifyserviceprovider.VerifyServiceProviderApplication'
